plugins {
  id("org.jetbrains.kotlin.jvm")
  id("maven-publish")
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
    }
  }
}

def agentClassName = "org.jetbrains.projector.agent.MainAgent"
def launcherClassName = "org.jetbrains.projector.agent.AgentLauncher"
version = project(":projector-plugin").version

kotlin {
  explicitApi()
}

compileKotlin {
  kotlinOptions.jvmTarget = targetJvm
}

dependencies {
  implementation("$projectorClientGroup:projector-common:$projectorClientVersion")
  implementation("$projectorClientGroup:projector-server-core:$projectorClientVersion")
  implementation("$projectorClientGroup:projector-util-loading:$projectorClientVersion")
  implementation("$projectorClientGroup:projector-util-logging:$projectorClientVersion")
  api(project(":projector-awt"))
  api(project(":projector-server"))
  implementation("org.javassist:javassist:$javassistVersion")
}

def inline = { deps -> deps.collect { it.isDirectory() ? it : zipTree(it) } }

Properties localProperties = new Properties()
if (project.rootProject.file('local.properties').canRead()) {
  localProperties.load(project.rootProject.file("local.properties").newDataInputStream())
}

def serverTargetClasspath = localProperties['projectorLauncher.targetClassPath']
def serverClassToLaunch = localProperties['projectorLauncher.classToLaunch']

jar {
  manifest {
    attributes(
      "Can-Redefine-Classes": true,
      "Can-Retransform-Classes": true,
      "Agent-Class": agentClassName,
      "Main-Class": launcherClassName,
      )
  }

  exclude("META-INF/versions/9/module-info.class")

  from {
    inline(configurations.runtimeClasspath)  // todo: remove
  }
}

//Server running tasks
println("----------- Agent launch config ---------------")
println("Classpath: $serverTargetClasspath")
println("ClassToLaunch: $serverClassToLaunch")
println("------------------------------------------------")
if (serverTargetClasspath != null && serverClassToLaunch != null) {
  task runWithAgent(type: JavaExec) {
    group = "projector"
    main = launcherClassName
    classpath(sourceSets.main.runtimeClasspath, jar, "$serverTargetClasspath")
    jvmArgs = [
      "-Djdk.attach.allowAttachSelf=true",
      "-Dswing.bufferPerWindow=false",
      "-Dorg.jetbrains.projector.agent.path=${project.file("build/libs/${project.name}-${project.version}.jar")}",
      "-Dorg.jetbrains.projector.agent.classToLaunch=$serverClassToLaunch",
    ]
    dependsOn(jar)
  }
}

def ideaPath = localProperties['projectorLauncher.ideaPath']
println("----------- Idea launch config ---------------")
println("Idea path: $ideaPath")
println("------------------------------------------------")
if (ideaPath != null) {
  def ideaLib = "$ideaPath/lib"
  def ideaClassPath = "$ideaLib/bootstrap.jar:$ideaLib/extensions.jar:$ideaLib/util.jar:$ideaLib/jdom.jar:$ideaLib/log4j.jar:$ideaLib/trove4j.jar:$ideaLib/jna.jar:$ideaPath/jbr/lib/tools.jar"
  def jdkHome = System.getProperty('java.home')

  println(jdkHome)

  task runIdeaWithAgent(type: JavaExec) {
    group = "projector"
    main = launcherClassName
    classpath(sourceSets.main.runtimeClasspath, jar, "$ideaClassPath", "$jdkHome/../lib/tools.jar")
    jvmArgs = [
      "-Djdk.attach.allowAttachSelf=true",
      "-Dswing.bufferPerWindow=false",
      "-Dorg.jetbrains.projector.agent.path=${project.file("build/libs/${project.name}-${project.version}.jar")}",
      "-Dorg.jetbrains.projector.agent.classToLaunch=com.intellij.idea.Main",
      "-Didea.paths.selector=ProjectorIntelliJIdea2019.3",
      "-Didea.jre.check=true",
      "-Didea.is.internal=true",
      "--add-exports=java.base/jdk.internal.vm=ALL-UNNAMED",
      "--add-opens=java.desktop/java.awt=ALL-UNNAMED",
      "--add-opens=java.desktop/sun.font=ALL-UNNAMED",
      "--add-opens=java.desktop/sun.awt=ALL-UNNAMED",
      "--add-opens=java.desktop/sun.swing=ALL-UNNAMED",
      "--add-opens=java.desktop/javax.swing=ALL-UNNAMED",
      "--add-opens=java.desktop/javax.swing.text.html=ALL-UNNAMED",
      "--add-opens=java.desktop/javax.swing.plaf.basic=ALL-UNNAMED",
      "--add-opens=java.base/java.lang=ALL-UNNAMED",
      "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED",
    ]
  }
}
